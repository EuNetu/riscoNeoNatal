/*
	Modelo para estimativa de risco de morte 
	neonatal

									Clara Nadini
									Euclides Neto
*/

FUNCTION_BLOCK riscoNascimento	// Bloco de definicao

VAR_INPUT				// Definindo variaveis de entrada
	peso : REAL;
	ig : REAL;
END_VAR

VAR_OUTPUT				// Definindo variaveis de saida
	risco : REAL;
END_VAR

FUZZIFY peso	
	TERM muitoBaixo := (800, 1) (1200, 1) (1400, 1) (1600, 0); 
	TERM baixo := (1400, 0) (1600, 1) (2200, 1) (2600, 0);
	TERM insuficiente := (2300, 0) (2600, 1) (2900, 1) (3100, 0);
	TERM normal := (2900, 0) (3200, 1) (4000, 1);
END_FUZZIFY

FUZZIFY ig
	TERM muitoPreTermo := (24, 1) (28, 1) (32,0) ;
	TERM preTermo := (29,0) (32,1) (34,1) (37.5, 0);
	TERM termo := (36, 0) (38, 1) (42, 1);
END_FUZZIFY

DEFUZZIFY risco			
	TERM muitoBaixo := (0,1) (4,1) (8,0); 
	TERM baixo := (6,0) (10, 1) (16,1) (22, 0);
	TERM poucoElevado := (18, 0) (25,1) (35,1) (55, 0);
	TERM elevado := (45, 0) (60, 1) (100, 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			
	ACT : MIN;			
	ACCU : MAX;			

	RULE 1 : IF peso IS muitoBaixo AND ig IS muitoPreTermo THEN risco IS elevado;
	RULE 2 : IF peso IS baixo AND ig IS muitoPreTermo THEN risco IS elevado; 
	RULE 3 : IF peso IS muitoBaixo AND ig IS preTermo THEN risco IS elevado;
	RULE 4 : IF peso IS baixo AND ig IS preTermo THEN risco IS poucoElevado;
	RULE 5 : IF peso IS insuficiente AND ig IS preTermo THEN risco IS baixo; 
	RULE 6 : IF peso IS normal AND ig IS preTermo THEN risco IS baixo;
	RULE 7 : IF peso IS muitoBaixo AND ig IS termo THEN risco IS poucoElevado;
	RULE 8 : IF peso IS baixo AND ig IS termo THEN risco IS baixo;
	RULE 9 : IF peso IS insuficiente AND ig IS termo THEN risco IS muitoBaixo; 
	RULE 10 : IF peso IS normal AND ig IS termo THEN risco IS muitoBaixo;
	
END_RULEBLOCK

END_FUNCTION_BLOCK